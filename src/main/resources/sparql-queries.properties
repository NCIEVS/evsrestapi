# constructClassCountsQuery(String namedGraph)
class.counts=SELECT ( count(?class) as ?count ) {  \
    GRAPH <#{namedGraph}> {  \
        ?class a owl:Class  \
  } \
}

# constructAllGraphNamesQuery()
all.graph.names=select distinct ?graphName where { graph ?graphName {?s ?p ?o} }

# constructAllGraphsAndVersionsQuery
all.graphs.and.versions=select ?graphName ?version ?date ?comment where { \
	graph ?graphName { \
		?o a owl:Ontology . \
		?o owl:versionInfo ?version . \
	    ?o dc:date ?date . \
	    ?o rdfs:comment ?comment . \
	} \
}

# constructVersionInfoQuery(String namedGraph)
version.info=SELECT ?version ?date ?comment { GRAPH <#{namedGraph}> { \
    ?o a owl:Ontology . \
    ?o owl:versionInfo ?version . \
    ?o dc:date ?date . \
    ?o rdfs:comment ?comment \
  } \
}

# constructAxiomQualifierQuery(String propertyCode, String namedGraph)
axiom.qualifier=SELECT DISTINCT ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?axiom a owl:Axiom . \
    ?axiom :#{propertyCode}?propertyValue \
  } \
} \
ORDER BY ?propertyValue

# constructPropertyQuery(String conceptCode, String namedGraph)
property=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?concept a owl:Class . \
    ?concept :NHC0 "#{conceptCode}" . \
    ?concept ?property ?propertyValue . \
    ?property a owl:AnnotationProperty . \
    ?property rdfs:label ?propertyLabel . \
    OPTIONAL { ?property :NHC0 ?propertyCode } \
  } \
}

# constructPropertyNoRestrictionsQuery(String conceptCode, String namedGraph)
property.no.restrictions=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?concept :NHC0 "#{conceptCode}" . \
    ?concept ?property ?propertyValue . \
    OPTIONAL { ?property rdfs:label ?propertyLabel }. \
    OPTIONAL { ?property :NHC0 ?propertyCode } \
  } \
}

# constructAllPropertyQuery(String conceptCode, String namedGraph)
all.property=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?concept :NHC0 "#{conceptCode}" . \
    ?concept ?property ?propertyValue . \
    OPTIONAL { ?property rdfs:label ?propertyLabel }. \
    OPTIONAL { ?property :NHC0 ?propertyCode } \
  } \
}

# constructConceptLabelQuery(String conceptCode, String namedGraph)
concept.label=SELECT ?conceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?concept :NHC0 "#{conceptCode}" . \
    ?concept rdfs:label ?conceptLabel  \
  } \
}

# constructAxiomQuery(String conceptCode, String namedGraph)
axiom=SELECT ?axiom ?axiomProperty  ?axiomValue \
{ GRAPH <#{namedGraph}> \
    { \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource :#{conceptCode} . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
} \
ORDER BY ?axiom

# constructSubconceptQuery(String conceptCode, String namedGraph)
subconcept=SELECT ?subclass ?subclassLabel ?subclassCode \
{ GRAPH <#{namedGraph}> \
        { \
          { \
            { \
              ?superclass a owl:Class . \
              ?superclass :NHC0 "#{conceptCode}" . \
              ?subclass rdfs:subClassOf ?superclass . \
              ?subclass a owl:Class . \
              ?subclass rdfs:label ?subclassLabel . \
              ?subclass :NHC0 ?subclassCode \
            } \
            FILTER (?superclass != ?subclass) \
          } \
          UNION \
          { \
            { \
              ?superclass a owl:Class . \
              ?superclass :NHC0 "#{conceptCode}" . \
              ?equiv_concept owl:intersectionOf ?list . \
              ?list rdf:rest*/rdf:first ?superclass . \
              ?subclass owl:equivalentClass ?equiv_concept . \
              ?subclass a owl:Class . \
              ?subclass rdfs:label ?subclassLabel . \
              ?subclass :NHC0 ?subclassCode \
            } \
            FILTER (?superclass != ?subclass) \
          } \
        } \
} \
ORDER by ?subclassLabel

# constructSuperconceptQuery(String conceptCode, String namedGraph)
superconcept=SELECT ?superclass ?superclassLabel ?superclassCode \
{ GRAPH <#{namedGraph}> \
        { \
          { \
            { \
              ?subclass a owl:Class . \
              ?subclass :NHC0 "#{conceptCode}" . \
              ?subclass rdfs:subClassOf ?superclass . \
              ?superclass a owl:Class . \
              ?superclass rdfs:label ?superclassLabel . \
              ?superclass :NHC0 ?superclassCode \
            } \
            FILTER (?subclass != ?superclass) \
          } \
          UNION \
          { \
            { \
              ?subclass a owl:Class . \
              ?subclass :NHC0 "#{conceptCode}" . \
              ?equiv_concept owl:intersectionOf ?list . \
              ?list rdf:rest*/rdf:first ?superclass . \
              ?subclass owl:equivalentClass ?equiv_concept . \
              ?superclass a owl:Class . \
              ?superclass rdfs:label ?superclassLabel . \
              ?superclass :NHC0 ?superclassCode \
            } \
            FILTER (?subclass != ?superclass) \
          } \
        } \
} \
ORDER by ?superclassLabel

# constructAssociationsQuery(String conceptCode, String namedGraph)
associations=SELECT ?relationshipCode ?relationship ?relatedConceptCode ?relatedConceptLabel \
{ \
    GRAPH <#{namedGraph}> \
    { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?y a owl:AnnotationProperty . \
        ?x ?y ?z . \
        ?z a owl:Class . \
        ?z rdfs:label ?relatedConceptLabel . \
        ?z :NHC0 ?relatedConceptCode . \
        ?y rdfs:label ?relationship . \
        ?y :NHC0 ?relationshipCode . \
        ?y rdfs:range ?y_range \
    } \
    FILTER (str(?y_range)=\"http://www.w3.org/2001/XMLSchema#anyURI\") \
} \
ORDER BY ?relationship ?relatedConceptLabel

# constructInverseAssociationsQuery(String conceptCode, String namedGraph)
inverse.associations=SELECT ?relationshipCode ?relatedConceptLabel ?relatedConceptCode ?relationship \
{ \
    GRAPH <#{namedGraph}> \
    { \
        ?z a owl:Class . \
        ?z rdfs:label ?z_label . \
        ?z :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?x a owl:Class . \
        ?x rdfs:label ?relatedConceptLabel . \
        ?x :NHC0 ?relatedConceptCode . \
        ?y a owl:AnnotationProperty . \
        ?x ?y ?z . \
        ?y rdfs:label ?relationship . \
        ?y :NHC0 ?relationshipCode . \
        ?y rdfs:range ?y_range \
    } \
    FILTER (str(?y_range)=\"http://www.w3.org/2001/XMLSchema#anyURI\") \
} \
ORDER BY ?relationship ?relatedConceptLabel

# constructInverseRolesQuery(String conceptCode, String namedGraph)
inverse.roles=SELECT distinct ?relatedConceptLabel ?relatedConceptCode ?relationship ?relationshipCode \
{ \
    GRAPH <#{namedGraph}> \
    { \
      { \
        ?x a owl:Class . \
        ?x :NHC0 ?relatedConceptCode . \
        ?x rdfs:label ?relatedConceptLabel . \
        ?x rdfs:subClassOf ?z0 . \
        ?z0 a owl:Class . \
        ?z0 owl:intersectionOf ?list . \
        ?list rdf:rest*/rdf:first ?z2 . \
        ?z2 a owl:Restriction . \
        ?z2 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z2 owl:someValuesFrom ?y . \
        ?y :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?y rdfs:label ?y_label \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 ?relatedConceptCode . \
        ?x rdfs:label ?relatedConceptLabel . \
        ?x rdfs:subClassOf ?r . \
        ?r a owl:Restriction . \
        ?r owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?r owl:someValuesFrom ?y . \
        ?y a owl:Class . \
        ?y :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string>  \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 ?relatedConceptCode . \
        ?x rdfs:label ?relatedConceptLabel . \
        ?x owl:equivalentClass ?z . \
        ?z a owl:Class . \
        ?z owl:intersectionOf ?list . \
        ?list rdf:rest*/rdf:first ?z2 . \
        ?z2 a owl:Restriction . \
        ?z2 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z2 owl:someValuesFrom ?y . \
        ?y a owl:Class . \
        ?y :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string>  \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 ?relatedConceptCode . \
        ?x rdfs:label ?relatedConceptLabel . \
        ?x owl:equivalentClass ?z1 . \
        ?z1 a owl:Class . \
        ?z1 owl:intersectionOf ?list1 . \
        ?list1 rdf:rest*/rdf:first ?z2 . \
        ?z2 owl:unionOf ?list2 . \
        ?list2 rdf:rest*/rdf:first ?z3 . \
        ?z3 owl:intersectionOf ?list3 . \
        ?list3 rdf:rest*/rdf:first ?z4 . \
        ?z4 a owl:Restriction . \
        ?z4 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z4 owl:someValuesFrom ?y . \
        ?y a owl:Class . \
        ?y :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string>  \
      } \
   } \
}  \
ORDER BY ?relationship ?relatedConceptLabel

# constructRolesQuery(String conceptCode, String namedGraph)
roles=SELECT distinct ?relationship ?relationshipCode ?relatedConceptLabel ?relatedConceptCode  \
{ \
    graph <#{namedGraph}> \
    { \
      { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?x rdfs:label ?x_label . \
        ?x rdfs:subClassOf ?z0 . \
        ?z0 a owl:Class . \
        ?z0 owl:intersectionOf ?list . \
        ?list rdf:rest*/rdf:first ?z2 . \
        ?z2 a owl:Restriction . \
        ?z2 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z2 owl:someValuesFrom ?y . \
        ?y :NHC0 ?relatedConceptCode . \
        ?y rdfs:label ?relatedConceptLabel \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?x rdfs:subClassOf ?r . \
        ?r a owl:Restriction . \
        ?r owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?r owl:someValuesFrom ?y . \
        ?y a owl:Class . \
        ?y rdfs:label ?relatedConceptLabel . \
        ?y :NHC0 ?relatedConceptCode \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?x owl:equivalentClass ?z . \
        ?z a owl:Class . \
        ?z owl:intersectionOf ?list . \
        ?list rdf:rest*/rdf:first ?z2 . \
        ?z2 a owl:Restriction . \
        ?z2 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z2 owl:someValuesFrom ?y . \
        ?y :NHC0 ?relatedConceptCode . \
        ?y rdfs:label ?relatedConceptLabel \
      } \
      UNION \
      { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}"^^<http://www.w3.org/2001/XMLSchema#string> . \
        ?x owl:equivalentClass ?z1 . \
        ?z1 a owl:Class . \
        ?z1 owl:intersectionOf ?list1 . \
        ?list1 rdf:rest*/rdf:first ?z2 . \
        ?z2 owl:unionOf ?list2 . \
        ?list2 rdf:rest*/rdf:first ?z3 . \
        ?z3 owl:intersectionOf ?list3 . \
        ?list3 rdf:rest*/rdf:first ?z4 . \
        ?z4 a owl:Restriction . \
        ?z4 owl:onProperty ?p . \
        ?p rdfs:label ?relationship . \
        ?p :NHC0 ?relationshipCode . \
        ?z4 owl:someValuesFrom ?y . \
        ?y :NHC0 ?relatedConceptCode . \
        ?y rdfs:label ?relatedConceptLabel \
      } \
   } \
} \
ORDER BY ?relationship ?relatedConceptLabel

# constructDisjointWithQuery(String conceptCode, String namedGraph)
disjoint.with=SELECT ?relatedConceptLabel ?relatedConceptCode ?relationship \
{ \
    GRAPH <#{namedGraph}> \
    { \
        ?x a owl:Class . \
        ?x :NHC0 "#{conceptCode}" . \
        ?x owl:disjointWith ?concept . \
        ?concept :NHC0 ?relatedConceptCode . \
        ?concept rdfs:label ?relatedConceptLabel \
        BIND (\"disjointWith\" as ?relationship) \
    } \
} \
ORDER BY ?relatedConceptLabel

# constructHierarchyQuery(String namedGraph)
hierarchy=SELECT ?parentCode ?parentLabel ?childCode ?childLabel \
    { \
    GRAPH <#{namedGraph}> \
      { \
        { \
          { \
            ?child a owl:Class . \
            ?child rdfs:label ?childLabel . \
            ?child :NHC0 ?childCode . \
            ?child rdfs:subClassOf ?parent . \
            ?parent a owl:Class . \
            ?parent rdfs:label ?parentLabel . \
            ?parent :NHC0 ?parentCode \
          } \
          FILTER (?child != ?parent) \
        } \
        UNION \
        { \
          { \
            ?child a owl:Class . \
            ?child rdfs:label ?childLabel . \
            ?child :NHC0 ?childCode . \
            ?child owl:equivalentClass ?y . \
            ?y a owl:Class . \
            ?y owl:intersectionOf ?list . \
            ?list rdf:rest*/rdf:first ?parent . \
            ?parent a owl:Class . \
            ?parent rdfs:label ?parentLabel . \
            ?parent :NHC0 ?parentCode \
          } \
          FILTER (?child != ?parent) \
        } \
      } \
    }

# constructAllPropertiesQuery(String namedGraph)
all.properties=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?property a owl:AnnotationProperty . \
    ?property rdfs:label ?propertyLabel . \
    ?property :NHC0 ?propertyCode . \
    ?property :P108 ?propertyValue . \
    filter not exists { ?property rdfs:range xml:anyURI } \
  } \
} \
ORDER BY ?propertyLabel

# constructAllAssociationsQuery(String namedGraph)
all.associations=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?property a owl:AnnotationProperty . \
    ?property rdfs:label ?propertyLabel . \
    ?property :NHC0 ?propertyCode . \
    ?property :P108 ?propertyValue . \
    ?property rdfs:range xml:anyURI  \
  } \
} \
ORDER BY ?propertyLabel

# constructAllRolesQuery(String namedGraph)
all.roles=SELECT ?property ?propertyCode  ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?property a owl:ObjectProperty . \
    ?property rdfs:label ?propertyLabel . \
    ?property :NHC0 ?propertyCode . \
    ?property :P108 ?propertyValue  \
  } \
} \
ORDER BY ?propertyLabel

# constructUniqueSourcesQuery(String namedGraph)
unique.sources=SELECT DISTINCT ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?axiom a owl:Axiom . \
    { ?axiom :P384 ?propertyValue } \
    UNION \
    { ?axiom :P386 ?propertyValue } \
  } \
} \
ORDER BY ?propertyValue