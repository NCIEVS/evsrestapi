
prefix.common=PREFIX owl:<http://www.w3.org/2002/07/owl#> \
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> \
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#> \
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#> \
PREFIX dc:<http://purl.org/dc/elements/1.1/> \
PREFIX oboInOwl:<http://www.geneontology.org/formats/oboInOwl#> \
PREFIX xml:<http://www.w3.org/2001/XMLSchema#>

prefix.graph=PREFIX :<#{source}#> \
PREFIX base:<#{source}>

prefix.chebi=PREFIX :<http://purl.obolibrary.org/obo/chebi.owl#> \
PREFIX base:<http://purl.obolibrary.org/obo/chebi.owl#> \
PREFIX chebi:<http://purl.obolibrary.org/obo/chebi/> \
PREFIX CHEBI:<http://purl.obolibrary.org/obo/chebi/> \
PREFIX obo:<http://purl.obolibrary.org/obo>

prefix.hgnc=PREFIX :<http://ncicb.nci.nih.gov/genenames.org/HGNC.owl#> \
PREFIX HGNC:<http://ncicb.nci.nih.gov/genenames.org/HGNC.owl#>

prefix.go=PREFIX obo1:<http://purl.obolibrary.org/obo> \
PREFIX GO:<http://purl.obolibrary.org/obo/go.owl>

all.graph.names=SELECT DISTINT ?graphName { graph ?graphName {?s ?p ?o} }

all.graphs.and.versions=SELECT DISTINCT ?source ?graphName ?version ?date \
{ GRAPH ?graphName \
  { \
    { \
      ?source a owl:Ontology . \
      ?source owl:versionInfo ?version \
      OPTIONAL { ?source (dc:date|oboInOwl:date) ?date } \
    } \
    UNION \
    { \
      ?source a owl:Ontology . \
      ?source owl:versionIRI ?version . \
      OPTIONAL { ?source (dc:date|oboInOwl:date) ?date } . \
      FILTER NOT EXISTS { ?source owl:versionInfo ?x_versionInfo } \
    } \
  } \
}

# ORDER BY is important here - used to build a list in order
axiom.qualifier=SELECT DISTINCT ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { ?x a owl:Axiom . \
    ?x #{propertyCode} ?propertyValue \
  } \
} \
ORDER BY ?propertyValue

properties=SELECT ?property ?propertyCode ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x #{codeCode} "#{conceptCode}" . \
      ?x ?property ?propertyValue . \
      ?property a owl:AnnotationProperty . \
      OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
      OPTIONAL { ?property #{codeCode} ?propertyCode } . \
      FILTER NOT EXISTS { ?property rdfs:range xml:anyURI } \
    } \
    UNION \
    { \
      ?x ?property ?propertyValue . \
      FILTER (?x IN (<#{conceptAbout}>)) . \
      ?property a owl:AnnotationProperty . \
      OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
      OPTIONAL { ?property #{codeCode} ?propertyCode } . \
      FILTER NOT EXISTS { ?property rdfs:range xml:anyURI } . \
      FILTER NOT EXISTS { ?x #{codeCode} ?x_code } \
    } \
  } \
}

properties.batch=SELECT ?conceptCode ?property ?propertyCode ?propertyLabel ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x a owl:Class . \
      ?x #{codeCode} ?conceptCode . \
      FILTER (?conceptCode IN (#{inClause})) . \
      ?x ?property ?propertyValue . \
      ?property a owl:AnnotationProperty . \
      OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
      OPTIONAL { ?property #{codeCode} ?propertyCode } . \
      FILTER NOT EXISTS { ?property rdfs:range xml:anyURI } \
    } \
    UNION \
    { \
      ?conceptCode a owl:Class . \
      FILTER (?conceptCode IN (#{aboutClause})) . \
      ?conceptCode ?property ?propertyValue . \
      ?property a owl:AnnotationProperty . \
      OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
      OPTIONAL { ?property #{codeCode} ?propertyCode } . \
      FILTER NOT EXISTS { ?property rdfs:range xml:anyURI } . \
      FILTER NOT EXISTS { ?conceptCode #{codeCode} ?x_code } \
    } \
  } \
}

# ORDER by is important here because we need all the axiom parts together
axioms=SELECT ?axiom ?axiomProperty ?axiomValue \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x a owl:Class . \
      ?x #{codeCode} "#{conceptCode}" . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
    UNION \
    { \
      ?x a owl:Class . \
      FILTER (?x IN (<#{conceptAbout}>)) . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
  } \
} \
ORDER BY ?axiom

# ORDER by is important here because we need all the axiom parts together
axioms.batch=SELECT ?conceptCode ?axiom ?axiomProperty ?axiomValue \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x a owl:Class . \
      ?x #{codeCode} ?conceptCode . \
      FILTER (?conceptCode IN (#{inClause})) . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
    UNION \
    { \
      ?x a owl:Class . \
      ?x #{codeCode} ?conceptCode . \
      FILTER (?x IN (#{aboutClause})) . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
  } \
} \
ORDER BY ?axiom

axioms.batch=SELECT ?conceptCode ?axiom ?axiomProperty ?axiomValue \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x a owl:Class . \
      ?x #{codeCode} ?conceptCode . \
      FILTER (?conceptCode IN (#{inClause})) . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
    UNION \
    { \
      ?x a owl:Class . \
      FILTER (?x IN (#{aboutClause})) . \
      ?axiom a owl:Axiom . \
      ?axiom owl:annotatedSource ?x . \
      ?axiom ?axiomProperty ?axiomValue \
    } \
  } \
} \
ORDER BY ?axiom

children=SELECT ?child ?childLabel ?childCode \
{ GRAPH <#{namedGraph}> \
  { \
    { \
       ?x_parent a owl:Class . \
       ?x_parent #{codeCode} "#{conceptCode}" . \
       ?child rdfs:subClassOf ?x_parent . \
       ?child a owl:Class . \
       OPTIONAL { ?child #{preferredNameCode} ?childLabel } . \
       OPTIONAL { ?child #{codeCode} ?childCode } . \
       FILTER (?x_parent != ?child) \
    } \
    UNION \
    { \
      ?x_parent a owl:Class . \
      ?x_parent #{codeCode} "#{conceptCode}" . \
      ?x_equiv_concept owl:intersectionOf ?y . \
      ?y rdf:rest*/rdf:first ?x_parent . \
      ?child owl:equivalentClass ?x_equiv_concept . \
      ?child a owl:Class . \
      OPTIONAL { ?child #{preferredNameCode} ?childLabel } . \
      OPTIONAL { ?child #{codeCode} ?childCode } . \
      FILTER (?x_parent != ?child) \
    } \
  } \
}

parents=SELECT ?parent ?parentLabel ?parentCode \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x_child a owl:Class . \
      ?x_child #{codeCode} "#{conceptCode}" . \
      ?x_child rdfs:subClassOf ?parent . \
      ?parent a owl:Class . \
      OPTIONAL { ?parent #{preferredNameCode} ?parentLabel } . \
      OPTIONAL { ?parent #{codeCode} ?parentCode } . \
      FILTER (?x_child != ?parent) \
    } \
    UNION \
    { \
      ?x_child a owl:Class . \
      ?x_child #{codeCode} "#{conceptCode}" . \
      ?x_equiv_concept owl:intersectionOf ?y2 . \
      ?y2 rdf:rest*/rdf:first ?parent . \
      ?x_child owl:equivalentClass ?x_equiv_concept . \
      ?parent a owl:Class . \
      OPTIONAL { ?parent #{preferredNameCode} ?parentLabel } . \
      OPTIONAL { ?parent #{codeCode} ?parentCode } . \
      FILTER (?x_child != ?parent) \
    } \
  } \
}

subset=SELECT ?concept ?conceptLabel ?conceptCode \
{ GRAPH <#{namedGraph}> \
  { \
    ?concept a owl:Class . \
    ?concept #{preferredNameCode} ?conceptLabel . \
    ?concept #{codeCode} ?conceptCode . \
    ?y a owl:AnnotationProperty . \
    ?concept ?y ?z . \
    ?z #{codeCode} "#{conceptCode}" . \
    ?y rdfs:label "Concept_In_Subset" . \
  } \
}

# ?concept and ?relatedConcept both must have a code
associations=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept #{codeCode} "#{conceptCode}" . \
    ?x_concept ?relationship ?relatedConcept . \
    ?relationship a owl:AnnotationProperty . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#    ?relationship rdfs:range ?y_range \
#  FILTER (str(?y_range)=\"http://www.w3.org/2001/XMLSchema#anyURI\") \
#ORDER BY ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
associations.batch=SELECT ?conceptCode ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept #{codeCode} ?conceptCode . \
    FILTER (?conceptCode IN (#{inClause})) . \
    ?x_concept ?relationship ?relatedConcept . \
    ?relationship a owl:AnnotationProperty . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#    ?relationship rdfs:range ?y_range \
#  FILTER (str(?y_range)="http://www.w3.org/2001/XMLSchema#anyURI") \
#ORDER BY ?conceptCode ?relationship ?relatedConceptLabel

associations.all=SELECT ?conceptCode ?conceptLabel ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    OPTIONAL { ?x_concept #{preferredNameCode} ?conceptLabel } . \
    ?x_concept #{codeCode} ?conceptCode . \
    ?x_concept ?relationship ?relatedConcept . \
    ?relationship a owl:AnnotationProperty . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}

# nothing optional for NCIt.
associations.all.ncit=SELECT ?conceptCode ?conceptLabel ?relationshipCode ?relationshipLabel ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept #{preferredNameCode} ?conceptLabel . \
    ?x_concept #{codeCode} ?conceptCode . \
    ?x_concept ?x_relationship ?x_relatedConcept . \
    ?x_relationship a owl:AnnotationProperty . \
    ?x_relatedConcept a owl:Class . \
    ?x_relatedConcept #{preferredNameCode} ?relatedConceptLabel . \
    ?x_relatedConcept #{codeCode} ?relatedConceptCode . \
    ?x_relationship #{preferredNameCode} ?relationshipLabel . \
    ?x_relationship #{codeCode} ?relationshipCode . \
  } \
}

# ?concept and ?relatedConcept both must have a code
inverse.associations=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptLabel ?relatedConceptCode \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept #{codeCode} "#{conceptCode}" . \
    ?relatedConcept ?relationship ?x_concept . \
    ?relationship a owl:AnnotationProperty . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#    ?relationship rdfs:range ?y_range \
#  FILTER (str(?y_range)=\"http://www.w3.org/2001/XMLSchema#anyURI\") \
#ORDER BY ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
inverse.associations.batch=SELECT ?conceptCode ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept #{codeCode} ?conceptCode . \
    FILTER (?conceptCode IN (#{inClause})) . \
    ?relatedConcept ?relationship ?x_concept . \
    ?relationship a owl:AnnotationProperty . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#    ?relationship rdfs:range ?y_range \
#  FILTER (str(?y_range)="http://www.w3.org/2001/XMLSchema#anyURI") \
#ORDER BY ?conceptCode ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
inverse.roles=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?relatedConcept a owl:Class . \
      ?relatedConcept (owl:equivalentClass|rdfs:subClassOf) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?x_concept . \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
    UNION \
    { \
      ?relatedConcept a owl:Class . \
      ?relatedConcept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?x_concept . \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
    UNION \
    { \
      ?relatedConcept a owl:Class . \
      ?relatedConcept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/ \
        ( (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first | \
          (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \
        ) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?x_concept . \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
  } \
}
#           (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \


# ?concept and ?relatedConcept both must have a code
inverse.roles.chebi=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptLabel ?relatedConceptCode \
{ GRAPH <#{namedGraph}> \
  { \
    ?relatedConcept a owl:Class . \
    ?relatedConcept rdfs:subClassOf ?z1 . \
    ?z1 a owl:Restriction . \
    ?z1 owl:onProperty ?relationship . \
    ?z1 owl:someValuesFrom ?x_concept . \
    ?x_concept a owl:Class . \
    ?x_concept #{codeCode} "#{conceptCode}" . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#ORDER BY ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
inverse.roles.batch=SELECT ?conceptCode ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
      ?relatedConcept a owl:Class . \
      ?relatedConcept (owl:equivalentClass|rdfs:subClassOf) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?x_concept . \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} ?conceptCode . \
      FILTER (?conceptCode IN (#{inClause})) . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#           (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \

# ?concept and ?relatedConcept both must have a code
roles=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptLabel ?relatedConceptCode  \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
    UNION \
    { \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
    UNION \
    { \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} "#{conceptCode}" . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/ \
        ( (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first | \
          (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \
        ) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
  } \
}
#          (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \
#ORDER BY ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
roles.chebi=SELECT ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptLabel ?relatedConceptCode  \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept rdfs:subClassOf ?rs . \
    ?x_concept #{codeCode} "#{conceptCode}" . \
    ?rs a owl:Restriction . \
    ?rs owl:onProperty ?relationship . \
    ?rs owl:someValuesFrom ?relatedConcept . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#ORDER BY ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
roles.batch=SELECT ?conceptCode ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptLabel ?relatedConceptCode \
{ GRAPH <#{namedGraph}> \
  { \
      ?x_concept a owl:Class . \
      ?x_concept #{codeCode} ?conceptCode . \
      FILTER (?conceptCode IN (#{inClause})) . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}
#          (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \
# ORDER BY ?conceptCode ?relationship ?relatedConceptLabel

# ?concept and ?relatedConcept both must have a code
roles.all=SELECT ?conceptCode ?conceptLabel ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x_concept a owl:Class . \
    ?x_concept (owl:equivalentClass|rdfs:subClassOf) ?rs . \
    ?rs a owl:Restriction . \
    ?rs owl:onProperty ?relationship . \
    ?rs owl:someValuesFrom ?relatedConcept . \
    ?relatedConcept a owl:Class . \
    OPTIONAL { ?x_concept #{preferredNameCode} ?conceptLabel } . \
    ?x_concept #{codeCode} ?conceptCode . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    ?relatedConcept #{codeCode} ?relatedConceptCode . \
    OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
    OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
  } \
}

# ?concept and ?relatedConcept both must have a code
roles.all.complex=SELECT ?conceptCode ?conceptLabel ?relationship ?relationshipCode ?relationshipLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      ?x_concept a owl:Class . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?x_concept #{preferredNameCode} ?conceptLabel } . \
      ?x_concept #{codeCode} ?conceptCode . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
    UNION \
    { \
      ?x_concept a owl:Class . \
      ?x_concept (owl:equivalentClass|rdfs:subClassOf)/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/ \
        ( (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first | \
          (owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first/(owl:unionOf|owl:intersectionOf)/rdf:rest*/rdf:first \
        ) ?rs . \
      ?rs a owl:Restriction . \
      ?rs owl:onProperty ?relationship . \
      ?rs owl:someValuesFrom ?relatedConcept . \
      ?relatedConcept a owl:Class . \
      OPTIONAL { ?x_concept #{preferredNameCode} ?conceptLabel } . \
      ?x_concept #{codeCode} ?conceptCode . \
      OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
      ?relatedConcept #{codeCode} ?relatedConceptCode . \
      OPTIONAL { ?relationship #{preferredNameCode} ?relationshipLabel } . \
      OPTIONAL { ?relationship #{codeCode} ?relationshipCode } . \
    } \
  } \
}

roles.all.complex.chebi=SKIP

# relationship label is bound to "disjointWith"
disjoint.with=SELECT ?relatedConcept ?relatedConceptLabel ?relatedConceptCode \
{ GRAPH <#{namedGraph}> \
  { \
    ?x a owl:Class . \
    ?x #{codeCode} "#{conceptCode}" . \
    ?x owl:disjointWith ?relatedConcept . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    OPTIONAL { ?relatedConcept #{codeCode} ?relatedConceptCode } . \
  } \
}

# relationship label is bound to "disjointWith"
disjoint.with.batch=SELECT ?conceptCode ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x a owl:Class . \
    ?x #{codeCode} ?conceptCode . \
    FILTER (?conceptCode IN (#{inClause})) . \
    ?x owl:disjointWith ?relatedConcept . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    OPTIONAL { ?relatedConcept #{codeCode} ?relatedConceptCode } . \
  } \
}
#ORDER BY ?conceptCode

# (1) owl:Class|rdfs:subClassOf|owl:Class
# (2) owl:Class|owl:equivalentClass|owl:Class|owl:intersectionOf|owl:Class
# (3) owl:Class|rdfs:subClassOf|owl:Class|owl:intersectionOf|owl:Class
hierarchy=SELECT ?parent ?parentCode ?parentLabel ?child ?childCode ?childLabel \
{ GRAPH <#{namedGraph}> \
  { \
    { \
      { \
        ?child a owl:Class . \
        ?child rdfs:subClassOf ?parent . \
        ?parent a owl:Class . \
        OPTIONAL { ?child #{preferredNameCode} ?childLabel } . \
        OPTIONAL { ?child #{codeCode} ?childCode } . \
        OPTIONAL { ?parent #{preferredNameCode} ?parentLabel } . \
        OPTIONAL { ?parent #{codeCode} ?parentCode } . \
        FILTER (!isBNode(?parent)) . \
        FILTER (!isBNode(?child)) \
      } \
    } \
    UNION \
    { \
      { \
        ?child a owl:Class . \
        ?child owl:equivalentClass ?y . \
        ?y a owl:Class . \
        ?y owl:intersectionOf ?y2 . \
        ?y2 rdf:rest*/rdf:first ?parent . \
        ?parent a owl:Class . \
        OPTIONAL {  ?child #{preferredNameCode} ?childLabel } . \
        OPTIONAL { ?child #{codeCode} ?childCode } . \
        OPTIONAL { ?parent #{preferredNameCode} ?parentLabel } . \
        OPTIONAL { ?parent #{codeCode} ?parentCode } . \
        FILTER (!isBNode(?parent)) . \
        FILTER (!isBNode(?child)) \
      } \
    } \
    UNION \
    { \
      { \
        ?child a owl:Class . \
        ?child rdfs:subClassOf ?y . \
        ?y a owl:Class . \
        ?y owl:intersectionOf ?y2 . \
        ?y2 rdf:rest*/rdf:first ?parent . \
        ?parent a owl:Class . \
        OPTIONAL { ?child #{preferredNameCode} ?childLabel } . \
        OPTIONAL { ?child #{codeCode} ?childCode } . \
        OPTIONAL { ?parent #{preferredNameCode} ?parentLabel } . \
        OPTIONAL { ?parent #{codeCode} ?parentCode } . \
        FILTER (!isBNode(?parent)) . \
        FILTER (!isBNode(?child)) \
      } \
    } \
  } \
}

# Get all properties with literal values not URI values
# limit to properties on owl:Class (can be expanded with UNION)
all.properties=SELECT DISTINCT ?property ?propertyCode ?propertyLabel \
{ GRAPH <#{namedGraph}> \
  { \
	?x_concept a owl:Class . \
    ?x_concept ?property ?y . \
    ?property a owl:AnnotationProperty . \
    OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
    OPTIONAL { ?property #{codeCode} ?propertyCode } . \
    FILTER NOT EXISTS { ?property rdfs:range xml:anyURI } \
  } \
}


# distinct property values (String properytCode, String namedGraph)
# for all concepts, regardless of status
distinct.property.values=SELECT DISTINCT ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { \
    ?x a owl:Class . \
    ?x #{propertyCode} ?propertyValue \
  } \
}

all.qualifiers=SELECT DISTINCT ?property ?propertyCode ?propertyLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?x owl:annotatedProperty ?y . \
    ?y a owl:AnnotationProperty . \
    ?property a owl:AnnotationProperty . \
    ?x ?property ?z . \
    OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
    OPTIONAL { ?property #{codeCode} ?propertyCode } . \
  } \
}
#ORDER BY ?propertyCode

all.properties.never.used=SELECT DISTINCT ?property ?propertyCode \
{ GRAPH <#{namedGraph}> \
  { \
    ?property a owl:AnnotationProperty . \
    OPTIONAL { ?property #{codeCode} ?propertyCode } . \
    FILTER NOT EXISTS { ?x ?property ?z } \
  } \
}
#ORDER BY ?propertyCode

# Annotation properties with URI range (to differentiate from properties)
all.associations=SELECT DISTINCT ?property ?propertyCode ?propertyLabel \
{ GRAPH <#{namedGraph}> \
  { ?property a owl:AnnotationProperty . \
    ?property rdfs:range xml:anyURI \
    OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
    OPTIONAL { ?property #{codeCode} ?propertyCode } . \
  } \
}

all.roles=SELECT DISTINCT ?property ?propertyCode ?propertyLabel \
{ GRAPH <#{namedGraph}> \
  { ?property a owl:ObjectProperty . \
    OPTIONAL { ?property #{preferredNameCode} ?propertyLabel } . \
    OPTIONAL { ?property #{codeCode} ?propertyCode } . \
  } \
}

# find all property values used for the specified axiom qualifier
# except those values used on retired concepts
axiom.qualifier.values=SELECT DISTINCT ?propertyValue \
{ GRAPH <#{namedGraph}> \
  { \
    ?x a owl:Axiom . \
    ?y a owl:Class . \
    ?x owl:annotatedSource ?y . \
    ?x #{propertyCode} ?propertyValue . \
    FILTER NOT EXISTS { ?y #{conceptStatusCode} "#{retiredStatusValue}" } \
  } \
} \
ORDER BY ?propertyValue

all.concepts.with.code=SELECT ?concept ?conceptCode ?conceptLabel \
{ GRAPH <#{namedGraph}> \
    { \
        ?concept a owl:Class . \
        OPTIONAL { ?concept #{preferredNameCode} ?conceptLabel } . \
        ?concept #{codeCode} ?conceptCode . \
    } \
} \
ORDER BY ?conceptCode 

# explicitly ignore owl deprecated concepts
all.concepts.without.code=SELECT ?concept ?conceptCode ?conceptLabel \
{ GRAPH <#{namedGraph}> \
    { \
        ?concept a owl:Class . \
        OPTIONAL { ?concept #{preferredNameCode} ?conceptLabel } . \
        FILTER NOT EXISTS { ?concept #{codeCode} ?conceptCode } . \
        FILTER NOT EXISTS { ?concept owl:deprecated "true"^^xsd:boolean } . \
        FILTER (!isBNode(?concept)) \
    } \
} \
ORDER BY ?x_conceptCode 

association.entries=SELECT DISTINCT ?concept ?conceptCode ?conceptLabel ?relatedConcept ?relatedConceptCode ?relatedConceptLabel \
{ GRAPH <#{namedGraph}> \
  { \
    ?concept a owl:Class . \
    ?y a owl:AnnotationProperty . \
    ?concept ?y ?relatedConcept . \
    ?relatedConcept a owl:Class . \
    ?y #{codeCode} "#{conceptCode}" . \
    OPTIONAL { ?concept #{preferredNameCode} ?conceptLabel } . \
    OPTIONAL { ?concept #{codeCode} ?conceptCode } . \
    OPTIONAL { ?relatedConcept #{preferredNameCode} ?relatedConceptLabel } . \
    OPTIONAL { ?relatedConcept #{codeCode} ?relatedConceptCode } . \
  } \
}
#     ?y rdfs:range ?y_range \
#  FILTER (str(?y_range)=\"http://www.w3.org/2001/XMLSchema#anyURI\") \
